#! /opt/perl/bin/perl

use Dancer;
use File::Find::Rule;
use Hash::Merge qw/ merge /;
use Modern::Perl;
use MooseX::amine;
use Template;
use Try::Tiny;

set appname      => 'MooseX::amine';
set charset      => "UTF-8";
set template     => "template_toolkit";
set logger       => 'console';
set log          => 'core';
set show_errors  => 1 ;
set access_log   => 1;
set warnings     => 1;

layout 'main';

our $VERSION = '0.1';

get '/_list' => sub {
  my @files = File::Find::Rule->file()->name('*.pm')->in('./');

  my %tree;
  foreach my $file ( @files  ) {
    my @parts = split '/' , $file;
    my $hash = { pop @parts => $file };
    while ( my $part = pop @parts ) {
      my $new_hash = { $part => $hash };
      $hash = $new_hash;
    }
    %tree = %{ merge( $hash , \%tree )  };
  }

  template 'list' => { files => \@files  , tree => \%tree };
};

get '/_show/name/*' => sub {
  my( $name ) = splat;
  debug $name;

  try {
    my $mex = MooseX::amine->new( $name );
    template 'show' => { mex => $mex->examine , name => $name };
  }
  catch { debug $name };

};

get qr{^/_show/(.*)} => sub {
  if ( my( $file ) = splat ) {
    debug $file;
    try {
      my $mex = MooseX::amine->new({ path => $file });
      template 'show' => { mex => $mex->examine , name => $mex->metaobj->name };
    }
    catch {
      if ($_ =~ /^Can't locate object method "meta" via package/ ) {
        template 'show' => { no_mex =>1 , message => 'Not a Moose object' };
      }
      else { die $_ }
    };
  }
};

get '/_show' => sub {
  template 'show' => { no_mex => 1 };
};

get '/' => sub { template 'index' => {} => { layout => 0 } };

Dancer->dance;
